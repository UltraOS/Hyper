# Do this because cmake attempts to link against -lc and crt0
set(CMAKE_TRY_COMPILE_TARGET_TYPE STATIC_LIBRARY)

if (HYPER_PLATFORM STREQUAL "uefi" AND HYPER_ARCH STREQUAL "amd64")
    set(HYPER_TRIPLET_PREFIX "x86_64")
else ()
    set(HYPER_TRIPLET_PREFIX ${HYPER_ARCH})
endif ()

if (HYPER_PLATFORM STREQUAL "uefi")
    set(HYPER_TRIPLET_POSTFIX "w64-mingw32")
elseif (HYPER_PLATFORM STREQUAL "bios")
    set(HYPER_TRIPLET_POSTFIX "elf")
else ()
    message(FATAL_ERROR "Platform ${HYPER_PLATFORM} is not supported")
endif ()

set(HYPER_COMPILER_PREFIX "${HYPER_TRIPLET_PREFIX}-${HYPER_TRIPLET_POSTFIX}")

set(HYPER_TOOLS_DIR "tools_${HYPER_ARCH}_${HYPER_PLATFORM}_${HYPER_TOOLCHAIN}")
set(HYPER_TOOLCHAIN_ROOT ${CMAKE_CURRENT_LIST_DIR}/${HYPER_TOOLS_DIR})
set(HYPER_TOOLCHAIN_PATH ${HYPER_TOOLCHAIN_ROOT}/bin)
set(HYPER_TOOLCHAIN_PREFIX ${HYPER_TOOLCHAIN_PATH}/${HYPER_COMPILER_PREFIX}-)

set(CMAKE_SYSTEM_NAME Linux)
set(CMAKE_SYSTEM_PROCESSOR ${HYPER_ARCH})
set(CMAKE_SYSROOT "")

set(CMAKE_C_LINK_EXECUTABLE
    "<CMAKE_LINKER> <CMAKE_C_LINK_FLAGS> <LINK_FLAGS> \
     <OBJECTS> -o <TARGET> <LINK_LIBRARIES>")

set(CMAKE_C_COMPILER ${HYPER_TOOLCHAIN_PREFIX}gcc)
set(CMAKE_LINKER ${HYPER_TOOLCHAIN_PREFIX}ld)
